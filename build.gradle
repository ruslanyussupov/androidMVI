// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext {

        // Kotlin
        kotlin_version = '1.4.10'
        kotlin_coroutines_version = '1.4.1'

        // Android
        desugar_jdk_libs_version = '1.1.0'
        android_lifecycle_version = '2.2.0'
        android_app_compat_version = '1.2.0'
        android_material_version = '1.2.1'
        constraint_layout_version = '2.0.2'
        androidx_core_ktx_version = '1.3.2'
        recyclerview_version = '1.1.0'
        activity_ktx_version = '1.1.0'

        // Utils
        debugDrawerVersion = '0.9.0'
        scalpelVersion = '1.1.2'

        // Testing
        junit_version = '4.12'

        depsWithVersion = [

                // Kotlin
                "org.jetbrains.kotlin:kotlin-stdlib"                : kotlin_version,
                "org.jetbrains.kotlinx:kotlinx-coroutines-core"     : kotlin_coroutines_version,
                "org.jetbrains.kotlinx:kotlinx-coroutines-android"  : kotlin_coroutines_version,

                // Android
                "com.android.tools:desugar_jdk_libs"                : desugar_jdk_libs_version,
                "androidx.appcompat:appcompat"                      : android_app_compat_version,
                "androidx.constraintlayout:constraintlayout"        : constraint_layout_version,
                "com.google.android.material:material"              : android_material_version,
                "androidx.recyclerview:recyclerview"                : recyclerview_version,
                "androidx.core:core-ktx"                            : androidx_core_ktx_version,
                "androidx.activity:activity-ktx"                    : activity_ktx_version,
                "androidx.lifecycle:lifecycle-viewmodel-ktx"        : android_lifecycle_version,
                "androidx.lifecycle:lifecycle-livedata-ktx"         : android_lifecycle_version,
                "androidx.lifecycle:lifecycle-runtime-ktx"          : android_lifecycle_version,
                "androidx.lifecycle:lifecycle-viewmodel-savedstate" : android_lifecycle_version,
                "androidx.lifecycle:lifecycle-common-java8"         : android_lifecycle_version,
                "androidx.lifecycle:lifecycle-extensions"           : android_lifecycle_version,

                // DebugDrawer
                "com.github.lenguyenthanh.debugdrawer:debugdrawer"                : debugDrawerVersion,
                "com.github.lenguyenthanh.debugdrawer:debugdrawer-base"           : debugDrawerVersion,
                "com.github.lenguyenthanh.debugdrawer:debugdrawer-no-op"          : debugDrawerVersion,
                "com.github.lenguyenthanh.debugdrawer:debugdrawer-commons"        : debugDrawerVersion,
                "com.github.lenguyenthanh.debugdrawer:debugdrawer-scalpel"        : debugDrawerVersion,
                "com.github.lenguyenthanh.debugdrawer:debugdrawer-timber"         : debugDrawerVersion,
                "com.github.lenguyenthanh.debugdrawer:debugdrawer-network-quality": debugDrawerVersion,

                // Utils
                "com.jakewharton.scalpel:scalpel"                   : scalpelVersion,

                // Testing
                "junit:junit"                                       : junit_version,
                "org.jetbrains.kotlin:kotlin-test-junit"            : kotlin_version,
                "org.jetbrains.kotlinx:kotlinx-coroutines-test"     : kotlin_coroutines_version,
        ]

        deps = { String key ->
            def version = depsWithVersion[key]
            if (version == null) throw new IllegalDependencyNotation("Version for library '$key' unknown. Update root build.gradle")
            "$key:${version}"
        }
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}